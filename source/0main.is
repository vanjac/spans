	.align  2
	.code   16
	.thumb_func
	.global	main
	.type   main, %function
main:
	@ setup interrupts

    LDR     reg_intr_handle:r0, =0x03007FFC
    LDR     intr_handle_ptr:r1, =intr_handle
    STR     intr_handle_ptr, [reg_intr_handle]

    LDR     reg_ie:r0, =0x04000200
    MOV     ie:r1, #1		@ vblank
    STRH    ie, [reg_ie]

    MOV     enable:r1, #1
    STRH    enable, [reg_ie, #0x08]         @ reg_ime

	@ setup screen

	LDR     reg_dispcnt:r0, =0x04000000
	LDR     value:r1, =(4 | (1<<10))
	STRH    value, [reg_dispcnt]
    LDR     vblank_irq:r1, =(1<<3)
    STRH    vblank_irq, [reg_dispcnt, #4]   @ reg_dispstat
	
	LDR     mem_palette:r0, =0x05000000
	LDR     color:r1, =0x7FFF		@ white
	STRH	color, [mem_palette, #2]
	LDR		color, =0x03E0			@ green
	STRH	color, [mem_palette, #4]

	LDR		transform_points_fn:r6, = transform_points
	LDR		draw_triangle_solid_fn:r7, =draw_triangle_solid
loop:
    LDR     cpuset_src:r0, =c_pal_green
    LDR     cpuset_dst:r1, =0x06000000
    LDR     cpuset_len:r2, =((240*160/4) | (1<<24))
    SWI     0x0C    @ CpuFastSet

	BL		transform_points_thumb

	LDR		point:r4, =transformed_points

	.rept	1
	MOV		r0, point
	ADD		point, #8
	MOV		r1, point
	ADD		point, #8
	MOV		r2, point
	ADD		point, #8
	LDR		r3, =0x01010101
	BL		draw_triangle_solid_thumb
	.endr

	NOP
	SWI		0x05		@ VBlankIntrWait
	B		loop

transform_points_thumb:
	BX		transform_points_fn

draw_triangle_solid_thumb:
	BX		draw_triangle_solid_fn

	.align	2
c_pal_green:
	.word	0x02020202


    .section    .iwram, "ax", %progbits
    .align  2
    .code   32
    .arm
intr_handle:
    LDR     reg_if:r0, =0x04000202
    LDRH    irq:r1, [reg_if]
    STRH    irq, [reg_if]       @ acknowledge interrupts
    LDR     reg_ifbios:r0, =0x03007FF8
    STRH    irq, [reg_ifbios]	@ bios
    BX      lr
